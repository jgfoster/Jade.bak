| package |
package := Package name: 'Jade Transcript'.
package paxVersion: 1;
	basicComment: ''.

package basicPackageVersion: '0.057'.


package classNames
	add: #AllSessionsPresenter;
	add: #JadeTranscript;
	add: #MySessionPresenter;
	add: #StoneInfoPresenter;
	add: #TranscriptPresenter;
	yourself.

package methodNames
	add: #JadeServer -> #_addSessionWithId:toStream:;
	add: #JadeServer -> #_streamType;
	add: #JadeServer -> #_stringType;
	add: #JadeServer -> #_systemConfigAsDictionary;
	add: #JadeServer -> #allSessions;
	add: #JadeServer -> #descriptionOfConfigOption:;
	add: #JadeServer -> #descriptionOfErrorNumber:;
	add: #JadeServer -> #mySessionInfo;
	add: #JadeServer -> #sendSigAbortToSession:;
	add: #JadeServer -> #sendSigUsr1ToSession:;
	add: #JadeServer -> #sleepAndCommit;
	add: #JadeServer -> #stoneInfo;
	add: #JadeServer -> #stopSession:;
	add: #JadeServer32bit -> #_addSessionWithId:toStream:;
	add: #JadeServer64bit -> #_addSessionWithId:toStream:;
	add: #JadeServer64bit -> #_streamType;
	add: #JadeServer64bit -> #_stringType;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: #(
	'..\Core\Object Arts\Dolphin\Base\Dolphin'
	'..\Core\Object Arts\Dolphin\MVP\Base\Dolphin Basic Geometry'
	'..\Core\Object Arts\Dolphin\MVP\Presenters\Boolean\Dolphin Boolean Presenter'
	'..\Core\Object Arts\Dolphin\MVP\Views\Cards\Dolphin Card Containers'
	'..\Core\Object Arts\Dolphin\MVP\Views\Common Controls\Dolphin Common Controls'
	'..\Core\Object Arts\Dolphin\MVP\Dialogs\Common\Dolphin Common Dialogs'
	'..\Core\Object Arts\Dolphin\MVP\Models\List\Dolphin List Models'
	'..\Core\Object Arts\Dolphin\MVP\Presenters\List\Dolphin List Presenter'
	'..\Core\Object Arts\Dolphin\Base\Dolphin Message Box'
	'..\Core\Object Arts\Dolphin\MVP\Base\Dolphin MVP Base'
	'..\Core\Object Arts\Dolphin\MVP\Presenters\Prompters\Dolphin Prompter'
	'..\Core\Object Arts\Dolphin\MVP\Presenters\Radio\Dolphin Radio Buttons'
	'..\Core\Object Arts\Dolphin\MVP\Views\Scintilla\Dolphin Scintilla View'
	'..\Core\Object Arts\Dolphin\MVP\Presenters\Text\Dolphin Text Presenter'
	'..\Core\Object Arts\Dolphin\MVP\Type Converters\Dolphin Type Converters'
	'..\Core\Object Arts\Dolphin\MVP\Models\Value\Dolphin Value Models'
	'GemStone C Interface'
	'GemStone Objects'
	'GemStone Session'
	'Jade Autocompletation'
	'Jade Inspector'
	'Jade Method Browser'
	'Jade UI Base').

package!

"Class Definitions"!

JadePresenterA subclass: #AllSessionsPresenter
	instanceVariableNames: 'autoUpdatePresenter autoUpdateProcess sessionListPresenter sessionListErrorPresenter'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
JadePresenterA subclass: #MySessionPresenter
	instanceVariableNames: 'configurationReportPresenter gciSessionIdPresenter gciVersionPresenter gemHostPresenter gemTypePresenter logPathPresenter netPortPresenter netTaskPresenter stoneHostPresenter stoneNamePresenter stoneSerialPresenter stoneSessionIDPresenter userIDPresenter versionReportPresenter'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
JadePresenterA subclass: #StoneInfoPresenter
	instanceVariableNames: 'configurationReportPresenter streamTypePresenter stringTypePresenter versionReportPresenter'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
CodeSourcePresenter subclass: #TranscriptPresenter
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!
JadeWorkspace subclass: #JadeTranscript
	instanceVariableNames: 'allSessionsPresenter historyPresenter mySessionPresenter stoneInfoPresenter toolbarPresenter transcriptPresenter'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

"Global Aliases"!


"Loose Methods"!

!JadeServer methodsFor!

_addSessionWithId: anInteger toStream: aStream

	| array gsSession timeGmt x |
	array := System descriptionOfSession: anInteger.
	array size: 20.
	gsSession := GsSession sessionWithSerialNumber: (array at: 9).
	timeGmt := System timeGmt.
	aStream
		nextPutAll: '<session oop=';
		nextPutAll: (self oopOf: gsSession) printString printString;
		nextPutAll: ' name=';
		nextPutAll: (array at: 1) userId printString;
		nextPutAll: ' process=';
		nextPutAll: (array at: 2) printString printString;
		nextPutAll: ' host=';
		nextPutAll: (array at: 3) printString;
		nextPutAll: ' primitive=';
		nextPutAll: (array at: 4) printString printString;
		nextPutAll: ' viewAge=';
		nextPutAll: (timeGmt - (array at: 5)) printString printString;
		nextPutAll: ' state=';
		nextPutAll: (array at: 6) printString printString;
		nextPutAll: ' transaction=';
		nextPutAll: (array at: 7) printString printString;
		nextPutAll: ' hasOldestCR=';
		nextPutAll: (array at: 8) printString printString;
		nextPutAll: ' serial=';
		nextPutAll: (array at: 9) printString printString;
		nextPutAll: ' id=';
		nextPutAll: (array at: 10) printString printString;
		nextPutAll: ' ip=';
		nextPutAll: (array at: 11) printString;
		nextPutAll: ' priority=';
		nextPutAll: ((x := array at: 12) isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' hostId=';
		nextPutAll: ((x := array at: 13)  isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' quietTime=';
		nextPutAll: ((x := array at: 14) isNil ifTrue: [''] ifFalse: [(timeGmt - x)  printString]) printString;
		nextPutAll: ' lifeTime=';
		nextPutAll: ((x := array at: 15) isNil ifTrue: [''] ifFalse: [(timeGmt - x)  printString]) printString;
		nextPutAll: ' backlog=';
		nextPutAll: ((x := array at: 16) isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' description=';
		nextPutAll: ((x := array at: 17) isNil ifTrue: [''] ifFalse: [x]) printString;
		nextPutAll: ' objects=';
		nextPutAll: ((x := array at: 18) isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' pages=';
		nextPutAll: ((x := array at: 19) isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' voteState=';
		nextPutAll: ((x := array at: 20) isNil ifTrue: [''] ifFalse: [x printString]) printString;
		nextPutAll: ' cacheDesc=';
		nextPutAll: ((x := (System gemCacheStatisticsForSessionId: anInteger) at: 1) isNil ifTrue: [''] ifFalse: [x]) printString;
		nextPutAll: ' />';
		yourself.
!

_streamType

	^'Legacy'!

_stringType

	^'String'!

_systemConfigAsDictionary

	| char dict i line list stream |
	list := Array new.
	stream := GsFile openReadOnServer: '$GEMSTONE/data/system.conf'.
	[
		[
			line := stream nextLine reject: [:each | each == Character cr or: [each == Character lf]].
			(2 < line size and: [(line copyFrom: 1 to: 2) = '#=']) ifTrue: [
				list add: self _writeStreamOnString.
			] ifFalse: [
				list last nextPutAll: line; cr.
			].
			stream atEnd not.
		] whileTrue: [].
	] ensure: [
		stream close.
	].
	list := list copyFrom: 3 to: list size.
	list := list collect: [:each | each contents].
	dict := Dictionary new.
	list do: [:each | 
		line := (ReadStream on: each) nextLine.
		line = '# End of Default GemStone Configuration Options' ifTrue: [^dict].
		(line copyFrom: 1 to: 2) = '# ' ifFalse: [line error: 'Unrecognized config file format!!'].
		i := 3.
		[
			i <= line size and: [(char := line at: i) == $_ or: [char isAlphaNumeric]].
		] whileTrue: [
			i := i + 1.
		].
		dict at: (line copyFrom: 3 to: i - 1) put: each.
	].
	self error: 'End of file not recognized!!'.!

allSessions

	| list stream string |
	stream := self _writeStreamOnString.
	stream nextPutAll: '<?xml version=''1.0'' ?><sessions>'.
	list := System currentSessionNames subStrings: Character lf.
	list := list reject: [:each | each isEmpty].
	list := list collect: [:each | (each subStrings at: 3) asNumber].
	list do: [:each | 
		self
			_addSessionWithId: each
			toStream: stream.
	].
	string := stream 
		nextPutAll: '</sessions>';
		contents.
	^self _encodeString: string!

descriptionOfConfigOption: aString

	| dict key string unicode |
	dict := self _systemConfigAsDictionary.
	unicode := self _decodeString: aString.
	(string := dict at: unicode ifAbsent: [nil]) notNil ifTrue: [^self _encodeString: string].	unicode := unicode asUppercase.
	dict keys do: [:each1 | 
		key := (each1 reject: [:each2 | each2 = $_]) asUppercase.
		key = unicode ifTrue: [^dict at: each1].
	].
	^''!

descriptionOfErrorNumber: anInteger

	| array stream |
	array := GemStoneError at: #'English'.
	anInteger <= 0 ifTrue: [^'Invalid number!!'].
	array size < anInteger ifTrue: [^'Invalid number!!'].
	stream := self _writeStreamOnString.
	array := array at: anInteger.
	array isNil ifTrue: [^'No entry in GemStoneError for #' , anInteger printString , '!!'].
	(array isKindOf: String) ifTrue: [array := Array with: array].
	array do: [:each | 
		(each isKindOf: Integer) ifTrue: [
			stream space; nextPut: $%.
			each printOn: stream.
		] ifFalse: [
			stream nextPutAll: each.
		].
	].
	^self _encodeString: stream contents.
!

mySessionInfo

	| dict stream |
	stream := self _writeStreamOnString.
	stream nextPutAll: self _gemLogPath; cr.
	dict := System gemVersionReport.
	dict keys asSortedCollection do: [:each | 
		stream nextPutAll: each; tab; nextPutAll: (dict at: each) asString; cr.
	].
	stream nextPut: $%; cr.
	dict := System gemConfigurationReport.
	dict keys asSortedCollection do: [:each | 
		stream nextPutAll: each; tab; nextPutAll: (dict at: each) asString; cr.
	].
	stream nextPut: $%; cr.
	^self _encodeString: stream contents!

sendSigAbortToSession: anInteger

	System sendSigAbortToSession: anInteger negated.
!

sendSigUsr1ToSession: anInteger

	| description command result |
	description := System descriptionOfSession: anInteger.
	command := 'kill -usr1 ' , (description at: 2) printString.
	result := System performOnServer: command.
	result trimSeparators notEmpty ifTrue: [self error: result trimSeparators].
!

sleepAndCommit

	[
		System commitTransaction.
	] whileTrue: [
		(Delay forSeconds: 30) wait.
	].
!

stoneInfo

	| dict stream |
	stream := self _writeStreamOnString
		nextPutAll: self _streamType; tab;
		nextPutAll: self _stringType; tab;
		cr;
		yourself.
	dict := System stoneVersionReport.
	dict keys asSortedCollection do: [:each | 
		stream nextPutAll: each; tab; nextPutAll: (dict at: each) asString; cr.
	].
	stream nextPut: $%; cr.
	dict := System stoneConfigurationReport.
	dict keys asSortedCollection do: [:each | 
		stream nextPutAll: each; tab; nextPutAll: (dict at: each) asString; cr.
	].
	stream nextPut: $%; cr.
	^self _encodeString: stream contents!

stopSession: anInteger

	System stopSession: anInteger.
! !
!JadeServer categoriesFor: #_addSessionWithId:toStream:!private!Sessions! !
!JadeServer categoriesFor: #_streamType!private!Transcript! !
!JadeServer categoriesFor: #_stringType!private!Transcript! !
!JadeServer categoriesFor: #_systemConfigAsDictionary!private!Transcript! !
!JadeServer categoriesFor: #allSessions!public!Sessions! !
!JadeServer categoriesFor: #descriptionOfConfigOption:!public!Transcript! !
!JadeServer categoriesFor: #descriptionOfErrorNumber:!public!Sessions! !
!JadeServer categoriesFor: #mySessionInfo!public!Transcript! !
!JadeServer categoriesFor: #sendSigAbortToSession:!public!Sessions! !
!JadeServer categoriesFor: #sendSigUsr1ToSession:!public!Sessions! !
!JadeServer categoriesFor: #sleepAndCommit!public!Transcript! !
!JadeServer categoriesFor: #stoneInfo!public!Transcript! !
!JadeServer categoriesFor: #stopSession:!public!Sessions! !

!JadeServer32bit methodsFor!

_addSessionWithId: anInteger toStream: aStream

	Exception
		category: nil
		number: nil
		do: [:ex :cat :num :args | '?????'].
	super
		_addSessionWithId: anInteger 
		toStream: aStream.
! !
!JadeServer32bit categoriesFor: #_addSessionWithId:toStream:!private!Sessions! !

!JadeServer64bit methodsFor!

_addSessionWithId: anInteger toStream: aStream

	[
		super
			_addSessionWithId: anInteger
			toStream: aStream.
	] on: Error do: [:ex | 
		ex resume: '?????'.
	].
!

_streamType

	| isLegacy type |
	type := Globals at: #'PositionableStream_position' ifAbsent: [#'Legacy'].
	(Globals includesKey: #'PositionableStreamLegacy') ifFalse: [^type].
	isLegacy := PositionableStream isLegacyStreamImplementation.
	(type = #'Legacy') == isLegacy ifTrue: [^type].
	self error: 'Inconsistent PositionableStream configuration'.
!

_stringType

	^(Globals at: #StringConfiguration ifAbsent: [String]) name
! !
!JadeServer64bit categoriesFor: #_addSessionWithId:toStream:!private!Sessions! !
!JadeServer64bit categoriesFor: #_streamType!private!Transcript! !
!JadeServer64bit categoriesFor: #_stringType!private!Transcript! !

"End of package definition"!

